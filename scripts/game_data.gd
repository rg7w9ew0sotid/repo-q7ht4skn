class_name GameData
extends RefCounted

const MANIFEST_PATH := "res://data/heroes.json"
const TOWER_BASE_HP := 1800.0
const LEGACY_CARD_TEXTURES := {
	"兽皮": "pelt",
	"木棒": "club",
	"投石": "sling",
	"兽骨": "bone",
	"石斧": "stone_axe",
	"长矛": "spear",
	"篝火": "campfire",
	"金块": "gold",
}

static var _manifest: Dictionary = {}
static var _initialized := false
static var ERAS: Array[String] = []
static var ERA_NAMES: Dictionary = {}
static var ERA_MULT: Dictionary = {}
static var ERA_UPGRADE_SCORE: Dictionary = {}
static var ROLES: Array[String] = []
static var ROLE_NAMES: Dictionary = {}
static var ROLE_BASE: Dictionary = {}
static var ROLE_SCALE: Dictionary = {}
static var HEROES: Dictionary = {}
static var HEROES_BY_ERA: Dictionary = {}
static var CARDS: Dictionary = {}

static func initialize() -> void:
	if _initialized:
		return
	_manifest = _load_manifest()
	ERAS = _string_array(_manifest.get("eras", []))
	ERA_NAMES = _manifest.get("era_names", {})
	ERA_MULT = _manifest.get("era_mult", {})
	ERA_UPGRADE_SCORE = _manifest.get("era_upgrade_score", {})
	ROLES = _string_array(_manifest.get("roles", []))
	ROLE_NAMES = _manifest.get("role_names", {})
	ROLE_BASE = _manifest.get("role_base", {})
	ROLE_SCALE = _manifest.get("role_scale", {})
	HEROES = _build_heroes(_manifest.get("heroes", []))
	HEROES_BY_ERA = _build_heroes_by_era(HEROES)
	CARDS = _build_cards(HEROES)
	_initialized = true

static func _load_manifest() -> Dictionary:
	if not FileAccess.file_exists(MANIFEST_PATH):
		push_error("找不到英雄 manifest: %s" % MANIFEST_PATH)
		return {}
	var parsed = JSON.parse_string(FileAccess.get_file_as_string(MANIFEST_PATH))
	if parsed is Dictionary:
		return parsed
	push_error("英雄 manifest 不是有效 JSON")
	return {}

static func _string_array(value: Variant) -> Array[String]:
	var result: Array[String] = []
	for item in value:
		result.append(str(item))
	return result

static func _build_heroes(raw_heroes: Variant) -> Dictionary:
	var result: Dictionary = {}
	for raw in raw_heroes:
		if not raw is Dictionary:
			continue
		var role := str(raw.get("role", "warrior"))
		var era := str(raw.get("era", "stone"))
		var base: Dictionary = ROLE_BASE.get(role, {})
		var mult := float(ERA_MULT.get(era, 1.0))
		var hero: Dictionary = raw.duplicate(true)
		hero["role"] = role
		hero["era"] = era
		hero["color_value"] = Color(str(raw.get("color", "#888888")))
		hero["scale"] = float(ROLE_SCALE.get(role, 1.0))
		hero["hp"] = float(base.get("hp", 100.0)) * mult
		hero["attack"] = float(base.get("attack", 10.0)) * mult
		hero["range"] = float(base.get("range", 46.0))
		hero["move_speed"] = float(base.get("move_speed", 40.0))
		hero["cooldown"] = float(base.get("cooldown", 1.0))
		hero["attack_speed"] = 1.0 / maxf(0.1, hero["cooldown"])
		hero["kill_score"] = int(base.get("kill_score", 10))
		hero["role_name"] = str(ROLE_NAMES.get(role, role))
		hero["era_name"] = str(ERA_NAMES.get(era, era))
		result[str(raw.get("id", ""))] = hero
	return result

static func _build_heroes_by_era(heroes: Dictionary) -> Dictionary:
	var result: Dictionary = {}
	for era in ERAS:
		result[era] = []
	for hero_id in heroes:
		var hero: Dictionary = heroes[hero_id]
		var era := str(hero.get("era", "stone"))
		if not result.has(era):
			result[era] = []
		result[era].append(hero_id)
	return result

static func _build_cards(heroes: Dictionary) -> Dictionary:
	var result: Dictionary = {}
	for hero_id in heroes:
		var hero: Dictionary = heroes[hero_id]
		var card_id := str(hero.get("card", hero_id))
		result[card_id] = {
			"name": card_id,
			"hero": hero_id,
			"unit": hero_id,
			"era": hero.get("era", "stone"),
			"color": hero.get("color_value", Color("#888888")),
		}
	return result

static func heroes_for_era(era: String) -> Array[String]:
	var result: Array[String] = []
	for hero_id in HEROES_BY_ERA.get(era, []):
		result.append(str(hero_id))
	return result

static func cards_for_era(era: String) -> Array[String]:
	var result: Array[String] = []
	for hero_id in heroes_for_era(era):
		result.append(str(HEROES[hero_id].get("card", hero_id)))
	return result

static func hero_for_card(card_id: String) -> Dictionary:
	var card: Dictionary = CARDS.get(card_id, {})
	return HEROES.get(card.get("hero", ""), {})

static func card_texture_path(card_id: String) -> String:
	var legacy_id: String = LEGACY_CARD_TEXTURES.get(card_id, "")
	if legacy_id != "":
		return "res://assets/cards/%s.png" % legacy_id
	return ""

static func hero_texture_path(hero_id: String) -> String:
	var hero: Dictionary = HEROES.get(hero_id, {})
	var anim_id := str(hero.get("anim", ""))
	var static_path := "res://assets/units/%s.png" % anim_id
	if ResourceLoader.exists(static_path):
		return static_path
	return ""

static func tower_hp(era: String) -> float:
	return TOWER_BASE_HP * float(ERA_MULT.get(era, 1.0))
